buildscript {
    repositories {
        /*
        maven {
            name = "fmm"
            url = "http://forgemavenmirror.sinaapp.com/maven"
        }
        */
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}
apply plugin: 'forge'

ext.rootConfigFile = file "../build.properties"
rootConfigFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.rootConfig = new ConfigSlurper().parse prop
}

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}
//Example: 14.4a-SNAPSHOT-20160820
version = rootConfig.version + rootConfig.dev_version + ((rootConfig.snapshot==""||rootConfig.snapshot=="0"||rootConfig.snapshot==0)?'':('-SNAPSHOT-'+System.getenv("BUILD_NUMBER")))
group = config.group
archivesBaseName = rootConfig.name+"_"+config.minecraft_version

minecraft {
    version = config.forge_mc_version+"-"+config.forge_version
    runDir = "run"
    mappings = config.mappings_version
    
    replace '@MOD_VERSION@', rootConfig.version
    replace '@MOD_FULL_VERSION@', project.version
    replaceIn 'CustomSkinLoader.java'
    
    /*
    replace '@MC_VERSION@', config.minecraft_version
    replace '@MC_FULL_VERSIONS@', config.minecraft_full_versions
    replace '@FORGE_VERSION@', config.forge_version
    replace '@GIT_VERSION@', System.getenv("GIT_VERSION")
    replace '@BUILD_URL@', System.getenv("BUILD_URL")
    replaceIn 'BuildInfo.json'*/
}

jar {
    manifest {
        attributes 'TweakClass'   : 'customskinloader.tweaker.ModSystemTweaker'
        attributes 'TweakName'    : rootConfig.name
        attributes 'TweakAuthor'  : 'xfl03,JLChnToZ'
        attributes 'TweakVersion' : version
        attributes 'TweakOrder'   : '-10'
    }
}

task copySource << {
    def outputFile = file("src/main/java")
    outputFile.deleteDir()
    copy {
        from '../Common'
        from 'src/minecraft/'
        into 'src/main/java'
		include '**/*.java'
    }
}
sourceMainJava.dependsOn copySource

task copyResources << {
    def outputFile = file("src/main/resources")
    outputFile.deleteDir()
    copy {
        from '../Common'
        into 'src/main/resources'
		exclude '**/*.java'
    }
}
processResources.dependsOn copyResources

task afterBuild << {
    //renameSrgJar
    def file0 = file("build/libs/${archivesBaseName}-${version}.jar")
    def file1 = file("build/libs/${archivesBaseName}-${version}-srg.jar")
    file0.renameTo(file1)
    
    //reobfSrgToNotch
    reobfSrgToNotch()
    
    //copyBuildFile
    copy {
        from 'build/libs'
        into '../build/libs'
		include '**/*.jar'
        exclude '**/*-srg.jar'
        exclude '**/*-sources.jar'
    }
}
build.finalizedBy afterBuild

//notch pack
void reobfSrgToNotch(){
    def srgName = "${archivesBaseName}-${version}-srg.jar"
    def mcpName = srgName.replace('-srg', '-dev')
    applySpecialSource(srgName,mcpName,"srg-mcp")
    def notchName = srgName.replace('-srg', '')
    applySpecialSource(mcpName,notchName,"mcp-notch")
}

void applySpecialSource(String inputName,String outputName,String srgType){
    def mcpVersion = minecraft.mappings.replace('_',"/")
    def mapping = new net.md_5.specialsource.JarMapping()
    def srgFile = file(System.getProperty('user.home') + "/.gradle/caches/minecraft/net/minecraftforge/forge/${config.forge_mc_version}-${config.forge_version}/${mcpVersion}/srgs/${srgType}.srg")
    def inputFile = file("build/libs/${inputName}")
    def outputFile = file("build/libs/${outputName}")
    def remapper = new net.md_5.specialsource.JarRemapper(null, mapping)
    def inputJar = net.md_5.specialsource.Jar.init(inputFile)
    def inheritanceProviders = new net.md_5.specialsource.provider.JointProvider()
    def jarProvider = new net.md_5.specialsource.provider.JarProvider(inputJar)
    inheritanceProviders.add(jarProvider)
    mapping.loadMappings(srgFile)
    mapping.setFallbackInheritanceProvider(inheritanceProviders)
    remapper.remapJar(inputJar, outputFile)
}
