buildscript {
    repositories {
        /*maven {
            name = "fmm"
            url = "http://forgemavenmirror.sinaapp.com/maven"
        }*/
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

ext.MINECRAFT_VERSION="1.10.2"
ext.FORGE_VERSION="12.18.2.2099"
ext.MAPPING_VERSION="stable_29"
ext.MINECRAFT_FULL_VERSIONS="1.8,1.8.8,1.8.9,1.9,1.9.4,1.10,1.10.2,1.11"

ext.snapshot = !(rootProject.ext.config.snapshot==""||rootProject.ext.config.snapshot=="0"||rootProject.ext.config.snapshot==0)

archivesBaseName = "CustomSkinLoader_Forge"
version = rootProject.ext.config.version + rootProject.ext.config.dev_version + (snapshot?"-SNAPSHOT-"+System.getenv("BUILD_NUMBER"):"")
group = "customskinloader"

sourceCompatibility = targetCompatibility = "1.6"
compileJava {
    sourceCompatibility = targetCompatibility = "1.6"
}
jar {
    manifest {
        attributes 'FMLCorePlugin': 'customskinloader.forge.ForgePlugin'
        attributes 'FMLCorePluginContainsFMLMod': true
    }
}
sourceSets {
    main {
        java {
            srcDir 'MergedSource'//Merge source
        }
        resources {
            srcDir 'MergedResource'
        }
    }
}

minecraft {
    version = MINECRAFT_VERSION+"-"+FORGE_VERSION
    runDir = "run"
    
    mappings = MAPPING_VERSION
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    replace '@MOD_VERSION@', rootProject.ext.config.version
    replace '@MOD_FULL_VERSION@', project.version
    replaceIn 'CustomSkinLoader.java'
    replaceIn 'ForgeMod.java'
}

task mergeSource { doLast {
    copy {
        from '../Common/source'
        from 'source'
        into 'MergedSource'
    }
}}
sourceMainJava.dependsOn mergeSource

task mergeResources { doLast {
    copy {
        from '../Common/resource'
        from 'resource'
        into 'MergedResource'
    }
}}
processResources.dependsOn mergeResources

processResources
{
    expand ([
        modVersion     :rootProject.ext.config.version,
        modFullVersion :project.version,
        mcVersion      :MINECRAFT_VERSION,
        mcFullVersions :MINECRAFT_FULL_VERSIONS,
        forgeVersion   :FORGE_VERSION,
        gitVersion     :System.getenv("GIT_COMMIT"),
        buildUrl       :System.getenv("BUILD_URL")
    ])
}

task afterBuild { doLast {
    //Clean Up
    def sourceDir = file("MergedSource")
    sourceDir.deleteDir()
    def resourceDir = file("MergedResource")
    resourceDir.deleteDir()

    //copyBuildFile
    copy {
        from 'build/libs'
        into '../build/libs'
        include '**/*.jar'
        exclude '**/*-sources.jar'
    }
}}
build.finalizedBy afterBuild
